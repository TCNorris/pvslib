tarski_examples: THEORY
BEGIN

  IMPORTING Tarski@strategies

%|- example_* : PROOF
%|- (tarski)
%|- QED

  example_1 : LEMMA
    FORALL (x:real): x^3 < 0 OR x*x*x > 0 OR x^2 = 0

  example_2 : LEMMA
    EXISTS (x:real) : x^3 = x AND x^2 = x

  example_3: LEMMA
    FORALL(y,x,z:real): (x-2)^2*(-x+4)>0 AND x^2*(x-3)^2>=0 AND x-1>=0 AND -(x-3)^2+1>0
    IMPLIES
    -(x-11/12)^3*(x-41/10)^3>=0 

  example_4: LEMMA
    FORALL (x:real): x>=-9 AND x<10 AND x^4>0 IMPLIES x^12>0

  example_5: LEMMA
    EXISTS (x:real): 
      (x-2)^2*(-x+4)>0 AND x^2*(x-3)^2>=0 AND x-1>=0 AND -(x-3)^2+1>0 AND
     -(x-11/12)^3*(x-41/10)^3<1/10

  example_6 : LEMMA
    EXISTS (x:real):
      x^5 - x - 1 = 0
      AND 
      x^12 + 425/23*x^11 - 228/23*x^10 - 2*x^8 
      - 896/23*x^7 - 394/23*x^6 + 456/23*x^5 + x^4 
      + 471/23*x^3 + 645/23*x^2 - 31/23*x - 228/23= 0 
      AND x^3 + 22*x^2 -31 >=0 
      AND x^22 -234/567*x^20 -419*x^10+ 1948>0

  example_7 : LEMMA
   FORALL (x:real): x^2 >= 1 IMPLIES abs(x) >= 1

  example_8:  LEMMA
    FORALL (x:real): (x^9 + 12*x^5 < 0 OR  x^2 >= 49 OR x^5 + 12*x^2 + 32*x = 0 OR x > 0)

  x : VAR real

  legendre_2(x:real) : MACRO real =
    (3*x^2-1)/2
       
  legendre_3(x:real) : MACRO real =
    (5*x^3-3*x)/2

  legendre_4(x:real) : MACRO real =
    (35*x^4-30*x^2+3)/8

  legendre_5(x:real) : MACRO real = 
    (63*x^5-70*x^3+15*x)/8
 
  legendre_6(x:real) : MACRO real =
    (231*x^6-315*x^4+105*x^2-5)/16

  Legendre_2_6 : LEMMA
    legendre_2(x) >= 0 OR legendre_3(x) >= 0 OR
    legendre_4(x) >= 0 OR legendre_5(x) >= 0 OR
    legendre_6(x) >= 0
%|- Legendre_2_6 : PROOF
%|- (tarski)
%|- QED

  IMPORTING vectors@vectors_2D 

  so : Vect2 = (-37040,0) % [m]
  soz: real  = 9144       % [m]^2
  vo : Vect2 = (218,218)  % [m/s]^2
  voz: real  = 2.54       % [m/s] 
  si : Vect2 = (37000,0)  % [m]^2
  siz: real  = 9000       % [m]
  vi : Vect2 = (-205,205) % [m/s]^2
  viz: real  = 2.5        % [m/s]
 
  s  : Vect2 = so-si
  sz : real  = soz-siz
  v  : Vect2 = vo-vi
  vz : real  = voz-viz

  D : real = 9260 % [m]
  H : real = 305  % [m]
  T : real = 300  % [s]

  conflict?(s,v:Vect2,T:real) : MACRO bool = 
    EXISTS (t:real | t ## [|0,T|]) : (s*s)+(2*s*v)*t+(v*v)*t^2 < sq(D) AND 
                                     abs(sz+t*vz) < H

  yes_conflict : LEMMA
    conflict?(so-si,vo-vi,300)
%|- yes_conflict : PROOF
%|- (tarski)
%|- QED

  no_conflict : LEMMA
    NOT conflict?(so-si,vo-vi,150)
%|- no_conflict : PROOF
%|- (tarski)
%|- QED

%|- *_TCC* : PROOF
%|- (subtype-tcc)
%|- QED

END tarski_examples
