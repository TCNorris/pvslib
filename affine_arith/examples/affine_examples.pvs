affine_examples: THEORY
BEGIN

  IMPORTING affine_arith@strategies
 
%|- *_num: PROOF
%|- (then (skeep)(aa-numerical (! 1 1)))
%|- QED
  
%|- *_num_p*_m*__nump: PROOF
%|- (then (skeep) (aa-numerical (! 1 1) :precision $2 :maxdepth $3))
%|- QED

%|- *_aa: PROOF
%|- (affine)
%|- QED

%|- *_aa_vrb: PROOF
%|- (affine :verbose? t)
%|- QED
 
  dlb, dub, lb, ub, x, y: VAR real

  zero_to_one_quarter(x, lb, ub): MACRO bool =  
    x ## [|0,1|] IMPLIES
      x*(1-x) ## [|lb, ub|]

  zero_to_one_quarter_num: LEMMA zero_to_one_quarter(x,0,0.251)

  zero_to_one_quarter_aa: LEMMA zero_to_one_quarter(x,0,0.2501)

  zero_to_one_quarter_sq_aa : LEMMA
    x ## [|0,1|] IMPLIES x-sq(x) < 0.2501

  example_1(x, dlb, dub, lb, ub): MACRO bool =
    x<dub AND x>dlb IMPLIES x^5-2*x^3 ## [|lb, ub|]

  example_1_num_p3_m9__nump: LEMMA example_1(x, -5, 0, -2875.000, 1.052)

  example_1_a_aa: LEMMA example_1(x, -5, 0, -2875.000, 1.052)

  example_1_num_p3_m16__nump: LEMMA example_1(x, -1000, 0, -999998000000000.000, 1.053)

  example_1_b_aa: LEMMA example_1(x, -1000, 0, -999998000000000.000, 1.053)

  example_legendre(x,lb,ub): MACRO bool =
    abs(x) < 1 IMPLIES
    3969/65536 + 63063/4096 * x^6 + 1792791/4096 * x^10 +
    3002285/4096 * x^18 + 6600165/4096 * x^14
    - 72765/65536 * x^4 - 3558555/32768 * x^8
    - 10207769/65536 * x^20 - 35043645/32768 * x^12
    - 95851899/65536 * x^16 ## [|lb, ub|]

  example_legendre_num_p3_m4__nump: LEMMA example_legendre(x, 0.000, 0.061)

  example_legendre_sint: LEMMA example_legendre(x, 0.000, 0.061)
%|- example_legendre_sint: PROOF
%|- (then (skeep)(affine))
%|- QED

  x0,x1,x2,x3,x4,x5,x6,x7 : VAR real

  Heart(x0,x1,x2,x3,x4,x5,x6,x7): MACRO real = 
    -x0*x5^3+3*x0*x5*x6^2-x2*x6^3+3*x2*x6*x5^2-x1*x4^3+3*x1*x4*x7^2-x3*x7^3+3*x3*x7*x4^2-0.9563453

  example_hdp_mm(x0,x1,x2,x3,x4,x5,x6,x7): MACRO bool =
    -10 <= x0 AND x0 <=  40 AND 
     40 <= x1 AND x1 <=  100   AND 
    -70 <= x2 AND x2 <= -40 AND 
    -70 <= x3 AND x3 <=  40 AND 
     10 <= x4 AND x4 <=  20 AND
    -10 <= x5 AND x5 <=  20 AND 
    -30 <= x6 AND x6 <=  110 AND 
    -110 <= x7 AND x7 <= -30 IMPLIES 
    Heart(x0,x1,x2,x3,x4,x5,x6,x7) ## [|-95100411.113, 247079452.169|]

  example_hdp_mm_num_p3_m10__nump: LEMMA example_hdp_mm(x0,x1,x2,x3,x4,x5,x6,x7)

  example_hdp_mm_aa: LEMMA example_hdp_mm(x0,x1,x2,x3,x4,x5,x6,x7)

%------- Examples with existencial quantifications ---------

  zero_to_one_quarter_e_aa_vrb : LEMMA  EXISTS(x:real):
    x ## [|0,1|] AND x*(1-x) ## [|0.0000, 0.2501|]

  zero_to_one_quarter_e_1_aa_vrb : LEMMA  EXISTS(x:real):
    x ## [|0,1|] AND x*(1-x) ## [| 0.199 , 0.201 |]

%---- Let

  zero_to_one_quarter__let_aa : LEMMA FORALL(x:real):
    x ## [|0,1|] IMPLIES
    LET y = (1-x) IN  x*y ## [|0.0000, 0.2501|]

END affine_examples
