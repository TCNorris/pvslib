affine_expr_Eval_fnd: THEORY
BEGIN
  IMPORTING affine_expr
  	  , structures@Maybe
	  , affine
	  , interval_arith@IntervalExpr
          , more_list_props

  re             : VAR RealExpr
  box,box1,box2  : VAR Box
  vs             : VAR Env

  CacheData: TYPE = [RealExpr            % A RealExpr (re)
  	     	    ,AffineCombination   % the AffineCombination (ac) s.t. 
		    			 %   FORALL vs: EXISTS N: ac[N] = re[vs]
		    ,ordered_list[nat,<]]% the list of new noises used in ac.

  CacheData_correct?(box)(cd: CacheData): bool
  = LET re   = cd`1, ac   = cd`2, idxs = cd`3 IN
    powvar?(re) 
    IMPLIES ( varidx_powvar(re) < length(box) AND
    	      LET varac = nth_ac(box,varidx_powvar(re)), 
    	      	  power = opn(re) IN
    	      ((length(idxs) > power-2 AND 
	       (NOT null?(idxs) IMPLIES car(idxs) >= next_idx(pd(varac))))
	      OR length(pd(varac)) = 0)
    	      AND ac = pow_var_ac(varac,power,idxs) )

  CacheData_correct?_inclusion: LEMMA FORALL(cd: CacheData):
  CacheData_correct?(box)(cd) 
  IMPLIES CacheData_correct?(append(box,box1))(cd)

  correct_cache?(box)(cache: list[CacheData]) : RECURSIVE bool
  = CASES cache OF 
    	  null: TRUE,
	  cons(h,t): CacheData_correct?(box)(h) AND correct_cache?(box)(t)
    ENDCASES
  MEASURE cache BY <<

  correct_cache?_alt_def: LEMMA FORALL (cache: list[CacheData]):
  correct_cache?(box)(cache) = every(CacheData_correct?(box))(cache)

  cache_powvar_correct: LEMMA
  FORALL(cache: (correct_cache?(box)), i: below(length(cache))):
  CacheData_correct?(box)(nth(cache,i))

  compatible_pairs_on?(box,vs)(N:Noise)(l: (correct_cache?(box))) : bool
  = FORALL(i: below(length(l)))(n: upfrom(length(box))):
    LET re = nth(l, i)`1, ac = nth(l, i)`2 IN
    eval_ac_noise(ac,N) = eval_ACExpr_Env(re,vs, n) AND
    ( powvar?(re) AND varidx_powvar(re) < length(box)
      IMPLIES pow_var_compatible?(nth_ac(box,varidx_powvar(re)),opn(re),nth(l, i)`3)(N))

  compatible_pairs?(box,vs)(l: (correct_cache?(box))) : bool
  = EXISTS(N: Noise): vars_in_box_compatible?(N)(box)(vs) AND
    compatible_pairs_on?(box,vs)(N)(l) 

  cache_eq(expr1,expr2:RealExpr) : INDUCTIVE bool =
    (const?(expr1) AND const?(expr2) AND opc(expr1)(unit) = opc(expr2)(unit) AND opC(expr1) = opC(expr2)) OR
    (varidx?(expr1) AND varidx?(expr2) AND varidx(expr1) = varidx(expr2)) OR
    (add?(expr1) AND add?(expr2) AND cache_eq(op1(expr1),op1(expr2)) AND cache_eq(op2(expr1),op2(expr2))) OR
    (abs?(expr1) AND abs?(expr2) AND cache_eq(op(expr1),op(expr2))) OR
    (neg?(expr1) AND neg?(expr2) AND cache_eq(op(expr1),op(expr2))) OR
    (sub?(expr1) AND sub?(expr2) AND cache_eq(op1(expr1),op1(expr2)) AND cache_eq(op2(expr1),op2(expr2))) OR
    (mult?(expr1) AND mult?(expr2) AND cache_eq(op1(expr1),op1(expr2)) AND cache_eq(op2(expr1),op2(expr2))) OR
    (sq?(expr1) AND sq?(expr2) AND cache_eq(op(expr1),op(expr2))) OR
    (pow?(expr1) AND pow?(expr2) AND cache_eq(op(expr1),op(expr2)) AND opn(expr1) = opn(expr2)) OR
    (div?(expr1) AND div?(expr2) AND cache_eq(op1(expr1),op1(expr2)) AND cache_eq(op2(expr1),op2(expr2)))

  cache_eq_sound : LEMMA
    FORALL (expr1,expr2:RealExpr) : cache_eq(expr1,expr2) => (expr1 = expr2)

  cache_get(cache: list[CacheData])(re)
  : RECURSIVE { res: Maybe[AffineCombination] | 
    some?(res) IMPLIES EXISTS(i:below(length(cache))): 
    	       	       	  nth(cache,i)`1 = re AND nth(cache,i)`2 = val(res) }
  = CASES cache OF
    	  null: None,
	  cons(h,t): IF cache_eq(h`1,re) THEN Some(h`2) ELSE cache_get(t)(re) ENDIF
    ENDCASES
  MEASURE cache BY <<

  cache_get_powvar(cache: list[CacheData])(re: (powvar?))
  : RECURSIVE 
  {res: Maybe[CacheData] | 
  	some?(res) IMPLIES 
		     powvar?(val(res)`1) AND
		     varidx_powvar(val(res)`1) = varidx_powvar(re) AND
    	      	     EXISTS(i:below(length(cache))): 
		      	  nth(cache,i) = val(res)}
  = CASES cache OF
     	  null: None,
  	  cons(h,t): IF NOT powvar?(h`1) THEN cache_get_powvar(t)(re)
   	  	     ELSIF varidx_powvar(h`1) /= varidx_powvar(re) THEN cache_get_powvar(t)(re)
   		     ELSIF opn(h`1) >= opn(re) THEN Some(h)
 		     ELSE LET nextresult = cache_get_powvar(t)(re) IN
  		     	  IF none?(nextresult) OR opn(val(nextresult)`1) < opn(h`1) THEN Some(h)
   			  ELSE nextresult ENDIF ENDIF
     ENDCASES
  MEASURE cache BY <<
  
  cache_powvar_correct_result: LEMMA
  FORALL(cache: list[CacheData], re: (powvar?)):
  correct_cache?(box)(cache) 
  IMPLIES LET cached = cache_get_powvar(cache)(re) IN
  	  some?(cached) IMPLIES CacheData_correct?(box)(val(cached))
 
  idxs_bounded(nextfreeidx: nat) (cache: list[CacheData]): bool
  = FORALL(i: below(length(cache))): 
	     nextfreeidx >= next_idx(pd(nth(cache,i)`2))

  RE2AC(re,box,vs,
	(nextfreeidx: nat | nextfreeidx>last_idx_in_box(box)),
	(cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache)
					      AND correct_cache?(box)(cache))) 
  : RECURSIVE {result: Maybe[[nat, (correct_cache?(box))]] |
    	       some?(result) 
	       IMPLIES LET res = val(result)`2, newnidx = val(result)`1 IN
    	      	    	   NOT null?(res) AND
			   car(res)`1 = re AND
		   	   (EXISTS(l: list[CacheData]): 
			     res = append(l, cache)) AND
    	      	    	   (EXISTS(N: Noise): 
		    	     vars_in_box_compatible?(N)(box)(vs) AND
    		    	     compatible_pairs_on?(box,vs)(N)(res))
			   AND newnidx >= nextfreeidx
			   AND idxs_bounded(newnidx)(res)
			   AND correct_cache?(box)(res)} 
  = CASES re OF
      CONST(opc, opC): LET result = (nextfreeidx, cons((re, const_ac(opc(unit)), null)::CacheData, cache)) 
      		       IN Some(result),
      VARIDX(varidx) : LET cached = cache_get(cache)(re) IN
      		       IF some?(cached) THEN
		       	  LET result = (nextfreeidx, cons((re,val(cached), null)::CacheData, cache)) IN Some(result)
		       ELSE
		          IF varidx >= length(box) THEN None
       		          ELSE LET ac     = nth_ac(box, varidx),
  		          	    	result = (nextfreeidx, cons((re,ac, null)::CacheData, cache)) IN
		          	    Some(result) 
		          ENDIF 
		       ENDIF,
      ADD(re1,re2)   : LET res1 = RE2AC(re1, box, vs, nextfreeidx, cache) IN
      		       IF none?(res1) THEN None
     		       ELSE LET newid1 = val(res1)`1,
		       	    	cache1 = val(res1)`2,
		       	    	ac1    = car(cache1)`2,
     				res2 = RE2AC(re2, box, vs, newid1, cache1) IN
     			    IF none?(res2) THEN None
     			    ELSE LET newid2 = val(res2)`1,
			    	     cache2 = val(res2)`2, 
			    	     ac2    = car(cache2)`2,
  				     result = (newid2, cons((re,add_ac_ac(ac1,ac2), null)::CacheData, cache2)) IN 
				 Some(result) ENDIF ENDIF,
      MULT(re1,re2)  : LET cached = cache_get(cache)(re) IN
      		       IF some?(cached) THEN
		       	  LET result = (nextfreeidx, cons((re,val(cached), null)::CacheData, cache)) IN Some(result)
		       ELSE
      		          LET res1 = RE2AC(re1, box, vs, nextfreeidx, cache) IN
      		       	  IF none?(res1) THEN None
     		       	  ELSE LET newid1 = val(res1)`1,
		       	    	   cache1 = val(res1)`2,
		       	    	   ac1    = car(cache1)`2,
     				   res2 = RE2AC(re2, box, vs, newid1, cache1) IN
     			       IF none?(res2) THEN None
     			       ELSE LET newid2 = val(res2)`1,
			    	     	cache2 = val(res2)`2, 
			    	     	ac2    = car(cache2)`2,
					result = ( newid2+1, 
					       	   cons( (re, mult_ac_ac(ac1,ac2,newid2), null)::CacheData
						       , cache2)) IN 
				    Some(result) ENDIF ENDIF ENDIF,
      POW(re1, pow)   : IF varidx?(re1) THEN
      	      	       LET idx = varidx(re1) IN
                       IF idx >= length(box) THEN None ELSE
       		       LET varac  = nth_ac(box, idx) IN
  		       % When pow<=1 or the range of the variable is a single va-
		       % lue, there is no need of new noise indexes.
     		       IF pow<=1 OR length(pd(varac)) = 0
  		       THEN LET acRes  = pow_var_ac(varac, pow, null_ol[nat,<]),
		       	    	result = (nextfreeidx, cons((re,acRes,null)::CacheData, cache)) 
			    IN Some(result)
		       ELSE LET cached = cache_get_powvar(cache)(re) IN
      		       	    IF % Exact match in cache
			       some?(cached) AND opn(val(cached)`1) = pow 
			    THEN LET result = (nextfreeidx, 
			    	     	       cons((re,val(cached)`2,val(cached)`3), cache)) 
			  	 IN Some(result)
			    ELSIF % A higher power than pow was found.
			    	  some?(cached) AND cv(varac) /= 0 AND opn(val(cached)`1) > pow 
			    THEN
			      LET idxs      = idxs_ac(val(cached)`2), 
			          pownoises = val(cached)`3,
			          acRes     = pow_var_ac(varac, pow, pownoises),
			          result    = (nextfreeidx, cons((re, acRes,pownoises), cache)) IN Some(result)
			    ELSIF % lower power than pow
			    	  some?(cached) AND cv(varac) /= 0 
			    THEN  % "opn(val(cached)`1) < pow" holds
			      LET idxs      = idxs_ac(val(cached)`2), 
			          pownoises = val(cached)`3,
			          newoff    = nextfreeidx+pow-opn(val(cached)`1),
  			          nwidxs    = IF opn(val(cached)`1)>=2 THEN 
				  	        append( take[nat](pownoises, opn(val(cached)`1)-2+1)
					      	      , nats(nextfreeidx, newoff-1)) 
				              ELSE nats(nextfreeidx, newoff-1) ENDIF,
			          acRes     = pow_var_ac(varac, pow, nwidxs),
			          result    = (newoff, cons((re, acRes, nwidxs), cache)) 
			      IN Some(result)
			    ELSE % No exponentiation on var idx was found.
			      LET newoff = nextfreeidx+(pow-2)+1,
  			       	  nwidxs = nats(nextfreeidx, newoff-1),
			      	  acRes  = pow_var_ac(varac, pow, nwidxs),
			      	  result = (newoff, cons((re, acRes,nwidxs), cache)) IN Some(result) ENDIF ENDIF ENDIF
		       ELSE None ENDIF,
      SUB(re1,re2)   : LET res1 = RE2AC(re1, box, vs, nextfreeidx, cache) IN
      		       IF none?(res1) THEN None
     		       ELSE LET newid1 = val(res1)`1,
		       	    	cache1 = val(res1)`2,
		       	    	ac1    = car(cache1)`2,
     				res2 = RE2AC(re2, box, vs, newid1, cache1) IN
     			    IF none?(res2) THEN None
     			    ELSE LET newid2 = val(res2)`1,
			    	     cache2 = val(res2)`2, 
			    	     ac2    = car(cache2)`2,
  				     result = (newid2, cons((re,sub_ac_ac(ac1,ac2), null)::CacheData, cache2)) IN 
				 Some(result) ENDIF ENDIF,
      NEG(re1)       : LET res1 = RE2AC(re1, box, vs, nextfreeidx, cache) IN
      		       IF none?(res1) THEN None
     		       ELSE LET newid1 = val(res1)`1,
		       	    	cache1 = val(res1)`2,
		       	    	ac1    = car(cache1)`2,
				result = (newid1, cons((re,neg_ac(ac1), null)::CacheData, cache1)) 
		 	    IN Some(result) ENDIF
      ELSE            None
    ENDCASES
  MEASURE re BY <<

  idxs_ac_cache_not_in_idx_gap(cache: list[CacheData], box: Box, first_added_idx: upfrom(last_idx_in_box(box)+1)): bool
  = ( FORALL(i: below(length(cache)))(j: nat): 
      member(j, idxs_ac(nth(cache, i)`2)) 
      IMPLIES (j < length(box) OR j >= first_added_idx) ) 

  iacniig_RE2AC_preservation: LEMMA
  FORALL (box: Box, vs: Env, re: RealExpr)
	 (first_added_idx: upfrom(last_idx_in_box(box)+1))
	 (nextfreeidx: upfrom(first_added_idx))
  	 (cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache)):
  LET RE2AC_ = RE2AC(re, box, vs, nextfreeidx, cache) IN
  idxs_ac_cache_not_in_idx_gap(cache, box, first_added_idx) AND
  some?(RE2AC_) 
  IMPLIES idxs_ac_cache_not_in_idx_gap(val(RE2AC_)`2, box, first_added_idx)
  
  iacniig_box_growing_preservation: LEMMA
  FORALL(cache: list[CacheData], box: Box, first_added_idx: above(last_idx_in_box(box)+1), I: Interval):
  idxs_ac_cache_not_in_idx_gap(cache, box, first_added_idx) 
  IMPLIES idxs_ac_cache_not_in_idx_gap(cache, append(box,(: I :)), first_added_idx)

  new_idxs_cache_not_in_idx_gap(cache: list[CacheData], box: Box, first_added_idx: upfrom(last_idx_in_box(box)+1)): bool
  = ( FORALL(i: below(length(cache)))(j: nat): 
      j < length(nth(cache, i)`3) 
      IMPLIES nth(nth(cache, i)`3, j) >= first_added_idx )

  nicniig_RE2AC_preservation: LEMMA
  FORALL (box: Box, vs: Env, re: RealExpr)
	 (first_added_idx: upfrom(last_idx_in_box(box)+1))
	 (nextfreeidx: upfrom(first_added_idx))
  	 (cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache)):
  LET RE2AC_ = RE2AC(re, box, vs, nextfreeidx, cache) IN
  new_idxs_cache_not_in_idx_gap(cache, box, first_added_idx) AND
  some?(RE2AC_) 
  IMPLIES new_idxs_cache_not_in_idx_gap(val(RE2AC_)`2, box, first_added_idx)

  nicniig_box_growing_preservation: LEMMA
  FORALL(cache: list[CacheData], box: Box, first_added_idx: above(last_idx_in_box(box)+1), I: Interval):
  new_idxs_cache_not_in_idx_gap(cache, box, first_added_idx) 
  IMPLIES new_idxs_cache_not_in_idx_gap(cache, append(box,(: I :)), first_added_idx) 

  varidxs_re_cache_in_box(cache: list[CacheData], box: Box): bool
  = ( FORALL(i: below(length(cache)))(subexpr: (varidx?)): subterm(subexpr, nth(cache,i)`1) IMPLIES varidx(subexpr) < length(box) )

  vrcib_RE2AC_preservation: LEMMA
  FORALL (box: Box, vs: Env, re: RealExpr)
  	 ((nextfreeidx: nat | nextfreeidx>last_idx_in_box(box)),
	  (cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache))):
  LET RE2AC_ = RE2AC(re, box, vs, nextfreeidx, cache) IN
  varidxs_re_cache_in_box(cache, box) AND
  some?(RE2AC_) 
  IMPLIES varidxs_re_cache_in_box(val(RE2AC_)`2, box)

  vrcib_box_growing_preservation: LEMMA
  FORALL(cache: list[CacheData], box: Box, I: Interval):
  varidxs_re_cache_in_box(cache, box) 
  IMPLIES varidxs_re_cache_in_box(cache, append(box,(: I :))) 

  RE2AC_box_vs_expansion: LEMMA
  FORALL (box: Box, vs: Env, re: RealExpr)
  	 ((nextfreeidx: nat | nextfreeidx>last_idx_in_box(box)),
	  (cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache)))
	  (I: Interval)
	  (x: real | EXISTS (e: Epsilon): (lb(I) + ub(I)) / 2  + e * ((ub(I) - lb(I)) / 2) = x )
	  (added_idx: above(last_idx_in_box(box)+1)): 
  nextfreeidx>=added_idx AND
  idxs_ac_cache_not_in_idx_gap(cache, box, added_idx) AND
  new_idxs_cache_not_in_idx_gap(cache, box, added_idx) AND
  varidxs_re_cache_in_box(cache, box)
  IMPLIES LET R1 = RE2AC(re, box, vs, nextfreeidx, cache),
      	      R2 = RE2AC( re
	      	   	, append(box,(: I :)) 
			, vs WITH [(length(box)) := x ]
			, nextfreeidx, cache ) 
  	  IN some?(R1) IMPLIES some?(R2) AND val(R1) = val(R2)

  cp_box_vs_expansion: LEMMA
  FORALL (box: Box, vs: Env)
	 (added_idx: above(last_idx_in_box(box)+1))
	 (nextfreeidx: upfrom(added_idx))
  	 (cache: (compatible_pairs?(box,vs)) | idxs_bounded(nextfreeidx)(cache))
	 (I: Interval)
	 (x: real | EXISTS (e: Epsilon): (lb(I) + ub(I)) / 2  + e * ((ub(I) - lb(I)) / 2) = x ):
  idxs_ac_cache_not_in_idx_gap(cache, box, added_idx)  AND
  varidxs_re_cache_in_box(cache, box) AND
  new_idxs_cache_not_in_idx_gap(cache, box, added_idx) 
  IMPLIES compatible_pairs?(append(box, (:I:)), vs WITH [(length(box)) := x])(cache)

END affine_expr_Eval_fnd
