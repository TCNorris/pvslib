%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%% This theory formalizes corollaries from the Rice's Theorem for the
%% multiple-function PVS0 model:
%% 
%% Undecidability of the Uniform Halting Problem
%% Undecidability of the existence of Fixed-Points
%% Undecidability of Self replication
%% Undecidability of Functional Equivalence of PVS0 programs
%% Undecidability of Natural Numbers Predicates
%%
%% Thiago Mendonca Ferreira Ramos, Ariane Alves Almeida Mauricio Ayala-Rincon
%% Universidade de Brasilia
%% Last modified: 29 May 2020 by Thiago MF Ramos
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mf_pvs0_Rices_Theorem_Corollaries 
		: THEORY

  BEGIN

  IMPORTING mf_pvs0_Rices_Theorem

   uniform_halting_problem_undecidability_Turing_complete: COROLLARY
    NOT EXISTS (oracle:computable): FORALL (pvs0:partial_recursive): 
      (NOT semantic_rel(oracle)(p_recursive2nat(pvs0), false_val)) IFF terminating?(pvs0)

    fixed_point_existence_undecidability_Turing_complete : COROLLARY
   (NOT EXISTS (decider : computable): FORALL (pvs0 : partial_recursive):
          (NOT semantic_rel(decider)(p_recursive2nat(pvs0), false_val)) IFF
   	  (EXISTS (p : nat) : semantic_rel(pvs0)(p,p)))

  self_replication_undecidability_Turing_complete : COROLLARY
     NOT EXISTS (decider : computable): FORALL (pvs0 : partial_recursive):
          (NOT semantic_rel(decider)(p_recursive2nat(pvs0), false_val)) IFF
   	  (EXISTS(p : partial_recursive):
   	  FORALL(i :nat) : semantic_rel(p)(i,p_recursive2nat(p)) AND semantic_rel(pvs0)(i,p_recursive2nat(p)) )

   pvs0_program_equivalence_undecidability_Turing_complete : COROLLARY
    (NOT EXISTS (decider : computable): FORALL (pvs0, pvs1 : partial_recursive):
        (NOT semantic_rel(decider)(tuple2nat(p_recursive2nat(pvs0),p_recursive2nat(pvs1)),false_val)) IFF
	(FORALL(i , o1 : nat) : semantic_rel(pvs0)(i,o1) <=> semantic_rel(pvs1)(i,o1)))
	
    natural_predicate_undecidability : COROLLARY
   NOT FORALL(P : pred[nat]): EXISTS(decider : computable):
   FORALL(i : nat): (NOT semantic_rel(decider)(i,false_val)) <=> P(i) 

  END mf_pvs0_Rices_Theorem_Corollaries
