%%-------------------** Abstract Reduction System (ARS) **-------------------
%%                                                                          
%% Authors         : Andre Luiz Galdino 
%%                   Universidade Federal de Goiás - Brasil
%%
%%                         and 
%%
%%                   Mauricio Ayala Rincon  
%%                   Universidade de Brasília - Brasil  
%%              
%% Last Modified On: February 13, 2019                                      
%%                                                                          
%%---------------------------------------------------------------------------


noetherian[T : TYPE] : THEORY
BEGIN

  IMPORTING ars_terminology[T], 
            orders@well_foundedness[T]
  
     P : VAR PRED[T]
     R, R1 : VAR PRED[[T, T]]
  x, y : VAR T


%%------------------------** Termination (Noetherian) **--------------------
%%
%% A reduction -> (relation R) is
%%
%% - noetherian (terminating) iff there is no infinite
%%   descending  chain a0 -> a1 -> .... In other words, a relation -> is 
%%   noetherian if the inverse (converse) relation of -> is well founded.
%%
%% - convergent if is confluent and noetherian.
%%
%% - R_is_Noet_iff_TC_is: ->+ is terminating iff -> is.
%%
%%
%% - noetherian_induction: To prove P(x) for all x, it suffices to prove P(x)
%%                         under the assunption that P(y) holds for all 
%%                         successors y of x. 
%%
%%--------------------------------------------------------------------------

 
 noetherian?(R): bool = well_founded?(converse(R))


 noetherian: TYPE = (noetherian?)


 convergent?(R): bool = confluent?(R) & noetherian?(R)


 R_is_Noet_iff_TC_is: LEMMA noetherian?(R) <=> noetherian?(TC(R))
 

 noetherian_induction: LEMMA 
             (FORALL (R: noetherian, P):
               (FORALL x: 
                  (FORALL y: TC(R)(x, y) IMPLIES P(y)) 
                      IMPLIES P(x))
            IMPLIES
              (FORALL x: P(x)))

%% Extra definitions and properties over noetherianity
%% of (subset) relations and elements. 
%% By Ariane A. Almeida
%%    Universidade de Brasília

  terminating?(R)(x): bool =
    NOT EXISTS (seq: sequence[T]):
    	  seq(0)=x AND
          FORALL (n: nat): R(seq(n), seq(1 + n))


  noetherian_iff_terminating_all: LEMMA
   noetherian?(R) IFF FORALL(x: T): terminating?(R)(x)


% Given a noetherian relation R1 that is subset of a non noetherian
% relation R, every infinite sequence of elements related with R have
% some index where the relation R1 does not hold.
non_noetherian_and_noetherian_subset : LEMMA 
 subset?(R1,R) AND noetherian?(R1) =>
   FORALL (seq: sequence[T]): 
      (FORALL (n: nat): R(seq(n), seq(1 + n))) IMPLIES
      EXISTS (i:nat) : NOT R1(seq(i),seq(i+1))           


END noetherian
