%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%% Notions and properties related with restriction of a relation to the
%% descending elements of a given input value. 
%% Author: Ariane Alves Almeida, Mauricio Ayala-Rincon
%%         Universidade de Brasilia
%% Last modified: 2nd Jan 2019
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


restricted_reduction[T : TYPE] : THEORY

BEGIN

IMPORTING noetherian[T]

R, R1: VAR pred[[T,T]] 
s, u, v : VAR T

% Predicate defining the elements obtained by derivations
% of a specific input s through a given relation R.
rest?(R, s)(u,v) : bool = 
  RTC(R)(s,u) AND RTC(R)(s,v) AND R(u,v)  

% Every restricted reduction is a subset of the original reduction.
rest_is_subset : LEMMA subset?(rest?(R,s), R)


% Monotony for restricted relations  
rest_of_subset_is_subset : LEMMA 
  subset?(R1, R) IMPLIES subset?(rest?(R1,s), rest?(R,s)) 


% Specialization of existence of index such that a noetherian relation subset
% of a non-noetherian relation does not hold in a infinite sequence where the 
% non-noetherian relation always hold.
non_noetherian_and_noetherian_rest : LEMMA 
 noetherian?(rest?(R,s)) =>
   FORALL (seq: sequence[T]): 
      (seq(0) = s AND 
       FORALL (n: nat): R(seq(n), seq(1 + n))) IMPLIES
      EXISTS (i:nat) : NOT rest?(R,s)(seq(i),seq(i+1))   

% Result similar to the previous one, but considering the restriction of a
% subset relation to a specific element as noetherian.
non_noetherian_and_noetherian_rest_subset : LEMMA 
 subset?(R1,R) AND noetherian?(rest?(R1,s)) =>
   FORALL (seq: sequence[T]): 
      (seq(0) = s AND 
       FORALL (n: nat): R(seq(n), seq(1 + n))) IMPLIES
      EXISTS (i:nat) : NOT rest?(R1,s)(seq(i),seq(i+1))   

         

END restricted_reduction
