%------------------------------------------------------------------------------
% Banach's fixed point theorem
%
%     Author: Aaron Dutle, NASA, 2019
%
%    A contraction mapping on a complete metric space always has a
%      unique fixed point. The nonempty set here is the subset of T 
%      which is the complete space. 
%
%------------------------------------------------------------------------------

banach[T:TYPE, (IMPORTING metric_def[T]) d:metric, S:nonempty_set[T] ]
		: THEORY

  BEGIN

  IMPORTING metric_space_def[T,d],
            series@series  % only used in proofs. In particular, that q^n -> 0, and sum_0^N (q^k) = (1-q^(N+1)/(1-q) 


    % Definition of a contraction mapping 
  contraction?(phi:[(S)->(S)]):
    bool = EXISTS (q: {qq: nnreal | qq<1}):
           FORALL (x,y:(S)): d(phi(x), phi(y)) <= q*d(x,y) 

    % Sequence of iterates of a contraction mapping 
  fun_iter(phi:[(S)->(S)])(x:(S))(n:nat):
    RECURSIVE (S) =
    IF n=0 THEN x
    ELSE phi(fun_iter(phi)(x)(n-1))
    ENDIF
    MEASURE n

    % The sequence of iterates is cauchy
  iter_cauchy: LEMMA
    FORALL (phi: (contraction?), x:(S)):
      complete_metric_space?(S) IMPLIES
      cauchy?(fun_iter(phi)(x))

    % There is a fixed point. 
  fixed_point_exists: LEMMA
    FORALL (phi: (contraction?), x:(S)):
    complete_metric_space?(S) IMPLIES
    EXISTS (x_star: (S)): phi(x_star) = x_star

    % The Banach Fixed Point Theorem.
    % Given a contraction mapping on a complete metric space:
    % a) There is a fixed point.
    % b) The fixed point is unique.
    % c) The fixed point is the limit of iterating the contraction starting 
    %    from any point in the space.  
  banach_fixed_point_thm: THEOREM
    FORALL  (phi: (contraction?)):
    complete_metric_space?(S) IMPLIES
    (EXISTS (x_star: (S)): phi(x_star) = x_star AND
    (FORALL (y: (S)): (phi(y) = y IMPLIES y = x_star)) AND
    (FORALL (x: (S)): metric_converges_to(fun_iter(phi)(x), x_star)))

  END banach
