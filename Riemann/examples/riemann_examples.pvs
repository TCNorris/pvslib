riemann_examples : THEORY
  BEGIN

  IMPORTING Riemann@strategies

  %% Examples demonstrating the use of riemann-numerical

  % More precision can be obtained by increasing the PRECISION argument, but
  % this will slow down the strategy.

  expcos : LEMMA integral(0, 1, exp * cos) ## [| 1.377, 1.379 |]
  %|- expcos : PROOF
  %|- (riemann-numerical)
  %|- QED

  % The default value of BREAKS is 12. Using fewer can speed up the strategy
  % but it will give less precise results

  sin_half_period : LEMMA integral(0, 3.14159, sin) ## [| 1.99, 2.01 |]
  %|- sin_half_period : PROOF
  %|- (riemann-numerical :breaks 10)
  %|- QED

  % idr can be used to refer to the integration variable

  square : LEMMA integral(0, 2, 3 * idr ^ 2) ## [| 7.99, 8.01 |]
  %|- square : PROOF
  %|- (riemann-numerical :breaks 11)
  %|- QED

  % The "o" operator can be used for composition. It is best to explicitly import
  % function_props when doing this even though it's in the prelude because it
  % allows you to fully instantiate o

  IMPORTING function_props[real, real, real]

  comp : LEMMA integral(0, 1, exp o cos) ## [| 2.33, 2.35 |]
  %|- comp : PROOF
  %|- (riemann-numerical :breaks 10)
  %|- QED

  %|- *_TCC1 : PROOF
  %|- (prove-integrable?)
  %|- QED

END riemann_examples
