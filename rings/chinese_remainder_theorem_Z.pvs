%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  The main result in this theory is the Chinese Remainder Theorem
%  for integers. 
%  The interesting point of this formalization is that it arises
%  as a Corollary of the algebraic version of the CRT using a
%  additional lemmas for integers.
%
%  Authors: Andre L. Galdino, Andreia Borges Avelar,
%           Thaynara de Lima, Mauricio Ayala-Rincon
%
%  Last modified: March 23, 2020
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

chinese_remainder_theorem_Z: THEORY

  BEGIN
    
    IMPORTING real_axioms,
              quotient_rings_with_one[int,+,*,0,1],
	      comaximal_finseqs_ideals[int,+,*,0,1],
              prop_primes_extra,
	      structures@seq2set,
	      algebra@ring_with_one_def,
	      ring_homomorphisms_def,
	      chinese_remainder_theorem_rings[int,+,*,0,1]

            d,n,m: VAR posnat
      a,b,c,x,y,k: VAR int
           fs,fsn: VAR finseq[posnat]
	      fsz: VAR finseq[nzint]
  
%-----------------------------------------------------------------------------

  fs_rel_primes(fsn): bool =
     FORALL (i,j: below[length(fsn)]): i /= j IMPLIES rel_prime(fsn(i),fsn(j))

  nZ(n): set[int] = {x | EXISTS k: x=k*n}

  nZ_add(n)(x,y:(nZ(n))): (nZ(n)) = x + y

  fsnZ(fsn): finseq[set[int]] = (# length := length(fsn),
                                      seq := (LAMBDA (i:below[length(fsn)]): nZ(fsn(i))) #)
  
  Z_ring: LEMMA ring?[int,+,*,0](fullset[int]);

  Z: ring[int,+,*,0] = fullset[int]

  Z1_is_Z: LEMMA nZ(1) = Z

  Z_commutative_ring_w_one: LEMMA commutative_ring_with_one?[int,+,*,0,1](Z)

  nZ_ideal: LEMMA FORALL n: ideal?(nZ(n),Z)

  Z(n): setofsets[int] = Z/nZ(n)

  Zn_finite_set: LEMMA is_finite(Z(n))

  Zn_card_n: LEMMA card(Z(n)) = n

  Zn_commutative_ring_w_one: LEMMA
    commutative_ring_with_one?[coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n),+(1,nZ(n))](Z(n))

  equal_cosets_div: LEMMA a + nZ(n) = b + nZ(n) IFF divides(n,b-a)

  nZ_mZ_sum: LEMMA sum(nZ(n),nZ(m)) = nZ(gcd(n,m))

  nZ_mZ_comaximal: LEMMA comaximal_ideals?(Z)(nZ(n),nZ(m)) IFF rel_prime(n,m)

  nZ_mZ_intersection: LEMMA LET lcm = n*m/gcd(n,m) IN
      intersection(nZ(n),nZ(m)) = nZ(lcm)

  nZ_mZ_rel_prime_intersection: COROLLARY
    rel_prime(n,m) IMPLIES intersection(nZ(n),nZ(m)) = nZ(n*m)

  Intersection_add_first: LEMMA
  FORALL (A: setof[int], fsS: finseq[setof[int]]): 
    Intersection(seq2set(add_first(A, fsS))) = intersection(A, Intersection(seq2set(fsS)))

  nZ_fs_intersection: LEMMA
  (length(fsn) /= 0 AND fs_rel_primes(fsn))
     IMPLIES
       LET fsnZ: finseq[set[int]] = (# length := length(fsn),
                                           seq:= (LAMBDA (i:below[length(fsn)]): nZ(fsn(i))) #) IN
       Intersection(seq2set(fsnZ)) = nZ(product(fsn))

% Main Theorem Chinese Remainder Theorem for integers
% The proof uses  nZ_fs_intersection, algebraic version of the CRT,
%                 nZ_ideal, nz_mz_comaximal, and Z_commutative_ring_w_one
%
Chinese_Remainder_Theorem_for_int: COROLLARY
   FORALL (fspn: finseq[posnat] | length(fspn) /= 0 AND fs_rel_primes(fspn)):
     R_isomorphic?[coset(Z, nZ(product(fspn))),
                   add(Z, nZ(product(fspn))),
   		   product(Z, nZ(product(fspn))),
   		   nZ(product(fspn)),
	   (cartesian_product_n[set[int]](fsQ(Z)(fsnZ(fspn)))), Sfs(Z)(fsnZ(fspn)), Pfs(Z)(fsnZ(fspn)),fsnZ(fspn)]
   	 (Z/nZ(product(fspn)), cartesian_product_n[set[int]](fsQ(Z)(fsnZ(fspn))))
  
  END chinese_remainder_theorem_Z

