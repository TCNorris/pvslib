
ring_ideal [T:TYPE+, +:[T,T -> T] , *:[T,T -> T], zero:T]: THEORY
	

 BEGIN

   ASSUMING IMPORTING algebra@ring_def[T,+,*,zero]
    
	fullset_is_ring: ASSUMPTION ring?(fullset[T])

   ENDASSUMING

   IMPORTING ring_basic_properties[T,+,*,zero],
             ring_ideal_def[T,+,*,zero]

   R: VAR ring
   H,I: VAR set[T]
   x: VAR T
 
    
   left_ideal_equiv: LEMMA	
         left_ideal?(I,R)
       IFF
	 nonempty?(I) AND subset?(I,R) AND
         FORALL (x,y:(I), r:(R)): member(x - y,I) AND member(r*x,I)

   right_ideal_equiv: LEMMA
         right_ideal?(I,R)
       IFF
         nonempty?(I) AND subset?(I,R) AND
	 FORALL (x,y:(I), r:(R)): member(x - y,I) AND member(x*r,I)

   ideal_equiv: LEMMA
         ideal?(I,R)
       IFF
         (nonempty?(I) AND subset?(I,R) AND
	 FORALL (x,y:(I), r:(R)): member(x - y,I) AND member(x*r,I)
	 		  	  AND member(r*x,I))

   self_ideal: LEMMA
         ideal?(R,R)

   zero_ideal: LEMMA
          ideal?(singleton(zero),R)
  
%%%%%%%%%%%%%%%%%%%%%%%%%
% remover desta teoria e, possivelmente, colocar em outro lugar ap√≥s definir normal_subgroup %%%% 

   % IMPORTING algebra@normal_subgroups[T,+,zero]

   % ideal_is_normal_subgroup: LEMMA
   %       ideal?(I,R) IMPLIES normal_subgroup?(I,R)
%%%%%%%%%%%%%%%%%%%%%%%%%

   ideal_transitive: LEMMA
         subring?(H,R) AND ideal?(I,R) AND subset?(I, H)
	 IMPLIES ideal?(I,H)

   intersection_subring_ideal: LEMMA
         subring?(H,R) AND ideal?(I,R)
         IMPLIES ideal?(intersection(H,I),H)


 END ring_ideal
