

comaximal_finseqs_ideals[T:Type+,+,*:[T,T->T],zero,one:T]: THEORY

BEGIN

    ASSUMING IMPORTING algebra@ring_with_one_def[T,+,*,zero,one]
    	fullset_is_ring: ASSUMPTION ring?(fullset[T])
	fullset_is_ring_with_one: ASSUMPTION
                                         ring_with_one?(fullset[T])
    ENDASSUMING
  

IMPORTING quotient_rings[T,+,*,zero],
          algebra@ring_with_one[T,+,*,zero,one],
          structures@seqs,
	  structures@seq2set,
	  structures@seq_extras

            R: VAR ring[T,+,*,zero]
	  M,N: VAR set[T]
          fsA: VAR finseq[set[T]]
   
%-------------------------------------------------

  comaximal_ideals?(R)(M,N): bool = ideal?(M,R) AND ideal?(N,R) AND sum(M,N) = R

  fsRI?(R)(fsA): bool = FORALL (i: below[length(fsA)]): ideal?(fsA(i), R)

  fsI(R): TYPE = {fsA: finseq[set[T]] | fsRI?(R)(fsA)}

  fsICM?(R)(fsA): bool = fsRI?(R)(fsA) AND
                           (FORALL (i,j: below[length(fsA)]):
                                        i /= j IMPLIES comaximal_ideals?(R)(fsA(i),fsA(j)))

  fsICM(R): TYPE = {fsA: finseq[set[T]] | fsICM?(R)(fsA)}

  fsQ(R)(fsA: fsI(R)): finseq[setof[set[T]]] =
          IF length(fsA) = 0
           THEN empty_seq
           ELSE
	     (# length := length(fsA),
                   seq := (LAMBDA (i:below[length(fsA)]): R/fsA(i)) #)
          ENDIF

%-------------------------------------------------

  comaximal_ideals_equiv: LEMMA
    FORALL (R: ring_with_one, M,N: ideal(R)):
      comaximal_ideals?(R)(M,N) IFF
                   (EXISTS (x,y: (R)): member(x,M) AND member(y,N) AND x+y = one)


END comaximal_finseqs_ideals
