sincos_def: THEORY
%----------------------------------------------------------------------------
%
% Definition of trigonometric functions
%
% Developed  by Ricky W. Butler      NASA Langley Research Center
% 
%      Version 1.0    last modified 10/02/00
%
%
%----------------------------------------------------------------------------

BEGIN

   IMPORTING series@power_series_conv, ints@factorial, analysis@deriv_domain

   AUTO_REWRITE- abs_0
   AUTO_REWRITE- abs_nat

   n: VAR nat
   x: VAR real

   c1,c2: VAR nat
   useful_prep: LEMMA convergent?(series(LAMBDA n: (abs(x))^n/factorial(n))) 


   altsign_prep: LEMMA (-1) ^ n = -1 OR (-1) ^ n = 1

   altsign(n: nat): {i: int | i = -1 OR i = 1} = IF even?(n) THEN (-1)^(n/2)
                                                 ELSE (-1)^((n-1)/2)
                                                 ENDIF

   abs_altsign: LEMMA abs(altsign(n)) = 1

   sin_coef(n): real = if even?(n) then 0 else altsign(n)/factorial(n) endif

   cos_coef(n): real = if even?(n) then altsign(n)/factorial(n) else 0 endif 


%  ----- convergent?(powerseries(sin_coef)(x)) ----

   sin_conv: LEMMA conv_powerseries?[real](sin_coef) 
                         
   sin(x): real = inf_sum(powerseq(sin_coef,x))

%  ----- convergent?(powerseries(cos_coef)(x)) ----

   cos_conv: LEMMA conv_powerseries?[real](cos_coef) 

   cos(x): real = inf_sum(powerseq(cos_coef,x))

   sin_0: LEMMA sin(0) = 0

   cos_0: LEMMA cos(0) = 1


   IMPORTING series@power_series_deriv[real], 
             analysis@derivative_props[real], analysis@deriv_domain


   derivable_sin: LEMMA derivable?(sin)

   deriv_sin    : THEOREM deriv(sin)(x) = cos(x)

   derivative_sin : LEMMA deriv(sin) = cos

   derivable_cos: LEMMA derivable?(cos)

   deriv_cos    : THEOREM deriv(cos)(x) = -sin(x)

   derivative_cos : LEMMA deriv(cos) = LAMBDA(x:real):-sin(x)

   sin2_cos2_derivable: LEMMA derivable?(sin*sin+cos*cos)

   sin2_cos2_fun    : THEOREM sin*sin + cos*cos = const_fun(1)

   sin_cos_one  : THEOREM sin(x)*sin(x) + cos(x)*cos(x) = 1

   sin_le_1     : LEMMA abs(sin(x)) <= 1

   cos_le_1     : LEMMA abs(cos(x)) <= 1

   a: VAR real
 
% -------------------- Pythagorean Property --------------------
 
   sin2_cos2    : LEMMA sq(sin(a)) + sq(cos(a)) = 1

   cos2         : LEMMA sq(cos(a)) = 1 - sq(sin(a)) 

   sin2         : LEMMA sq(sin(a)) = 1 - sq(cos(a)) 

% --------------------- Basic Values ----------------------------

   % cos_0         : LEMMA cos(0) = 1 

   % sin_0         : LEMMA sin(0) = 0

   % sin_pi2       : LEMMA sin(pi/2) = 1

   % cos_pi2       : LEMMA cos(pi/2) = 0


% --------------------- Negative Properties ----------------------

  pow_neg_prep: LEMMA
    even?(n) IMPLIES (-x)^n = x^n

  sin_neg : LEMMA sin(-a) = -sin(a) 
  cos_neg : LEMMA cos(-a) = cos(a)

  AUTO_REWRITE- sin,cos 

END sincos_def


