
ring_with_one_basic_properties[T:Type+,+,*:[T,T->T],zero:T,one:T]: THEORY

BEGIN


   ASSUMING IMPORTING algebra@ring_with_one_def[T,+,*,zero,one]		     
  	   	
     fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING


 IMPORTING algebra@monoid_def[T,*,one],
 	   algebra@ring_with_one[T,+,*,zero,one],
           ring_basic_properties[T,+,*,zero],
	   cosets_def[T,*]


 x,y : VAR T
 i,m : VAR nat
   k : VAR int
   R : VAR ring_with_one

%%----------------------------------------------------------------------------
%%  Results about the power[T,*,one] function
%%----------------------------------------------------------------------------

 power_commute_aux: LEMMA
     x*y = y*x IMPLIES
     x*power(y,m) = power(y,m)*x

 power_commute: LEMMA
     x*y = y*x IMPLIES
     power(x,m)*power(y,i) = power(y,i)*power(x,m)

%%----------------------------------------------------------------------------

 gen_times_int_one: LEMMA
     times(one,k) = zero
     IMPLIES times(x, k) = zero

 ring_w_one_is_idempotent: LEMMA
     sum[T,*](R,R) = R

 one_diff_zero_monad: LEMMA
 monad?[T,*,one](remove(zero,R)) IMPLIES one /= zero

END ring_with_one_basic_properties
%%----------------------------------------------------------------------------
